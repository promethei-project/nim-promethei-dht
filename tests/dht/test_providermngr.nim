import std/sequtils
import std/times

import pkg/chronos
import pkg/asynctest/chronos/unittest2
import pkg/kvstore
import pkg/taskpools
from pkg/libp2p import PeerId

import prometheidht/private/eth/p2p/discoveryv5/spr
import prometheidht/private/eth/p2p/discoveryv5/providers
import prometheidht/discv5/node
import prometheidht/private/eth/p2p/discoveryv5/lru
import prometheidht/private/eth/p2p/discoveryv5/random2
import ./test_helper

suite "Test Providers Manager simple":
  var tp = Taskpool.new(num_threads = 4)
  let
    ds = SQLiteKVStore.new(SqliteMemory, tp).tryGet()
    manager = ProvidersManager.new(ds, disableCache = true)
    rng = newDrbg()
    privKey = PrivateKey.example(rng)
    provider = privKey.toSignedPeerRecord()
    nodeId = NodeId.example(rng)

  teardownAll:
    (await ds.close()).tryGet()
    tp.shutdown()

  test "Should add provider":
    (await manager.add(nodeId, provider)).tryGet

  test "Should get provider":
    let prov = (await manager.get(nodeId)).tryGet

    check prov[0] == provider

  test "Should check provider presence":
    check:
      (await manager.contains(nodeId))
      (await manager.contains(provider.data.peerId))
      (await manager.contains(nodeId, provider.data.peerId))

  test "Should update provider with newer seqno":
    var updated = provider

    updated.incSeqNo(privKey).tryGet
    (await manager.add(nodeId, updated)).tryGet
    let prov = (await manager.get(nodeId)).tryGet
    check prov[0] == updated

  test "Should remove single record by NodeId and PeerId":
    check:
      (await manager.contains(nodeId))
      (await manager.contains(provider.data.peerId))

    (await (manager.remove(nodeId, provider.data.peerId))).tryGet

    check:
      not (await manager.contains(nodeId, provider.data.peerId))

suite "Test Providers Manager multiple":
  let
    rng = newDrbg()
    privKeys = (0 ..< 10).mapIt(PrivateKey.example(rng))
    providers = privKeys.mapIt(it.toSignedPeerRecord())
    nodeIds = (0 ..< 100).mapIt(NodeId.example(rng))

  var
    tp: Taskpool
    ds: SQLiteKVStore
    manager: ProvidersManager

  setup:
    tp = Taskpool.new(num_threads = 4)
    ds = SQLiteKVStore.new(SqliteMemory, tp).tryGet()
    manager = ProvidersManager.new(ds, disableCache = true)

    for id in nodeIds:
      for p in providers:
        (await manager.add(id, p)).tryGet

  teardown:
    (await ds.close()).tryGet()
    tp.shutdown()
    ds = nil
    manager = nil

  test "Should retrieve multiple records":
    for id in nodeIds:
      check:
        (await manager.get(id)).tryGet.len == 10

  test "Should retrieve multiple records with limit":
    for id in nodeIds:
      check:
        (await manager.get(id, 5)).tryGet.len == 5

  test "Should remove by NodeId":
    (await (manager.remove(nodeIds[0]))).tryGet
    (await (manager.remove(nodeIds[49]))).tryGet
    (await (manager.remove(nodeIds[99]))).tryGet

    check:
      not (await manager.contains(nodeIds[0]))
      not (await manager.contains(nodeIds[49]))
      not (await manager.contains(nodeIds[99]))

  test "Should remove by PeerId with associated keys":
    (await (manager.remove(providers[0].data.peerId, true))).tryGet
    (await (manager.remove(providers[5].data.peerId, true))).tryGet
    (await (manager.remove(providers[9].data.peerId, true))).tryGet

    for id in nodeIds:
      check:
        not (await manager.contains(id, providers[0].data.peerId))
        not (await manager.contains(id, providers[5].data.peerId))
        not (await manager.contains(id, providers[9].data.peerId))

    check:
      not (await manager.contains(providers[0].data.peerId))
      not (await manager.contains(providers[5].data.peerId))
      not (await manager.contains(providers[9].data.peerId))

  test "Should not return keys without provider":
    for id in nodeIds:
      check:
        (await manager.get(id)).tryGet.len == 10

    for provider in providers:
      (await (manager.remove(provider.data.peerId))).tryGet

    for id in nodeIds:
      check:
        (await manager.get(id)).tryGet.len == 0

    for provider in providers:
      check:
        not (await manager.contains(provider.data.peerId))

suite "Test providers with cache":
  let
    rng = newDrbg()
    privKeys = (0 ..< 10).mapIt(PrivateKey.example(rng))
    providers = privKeys.mapIt(it.toSignedPeerRecord())
    nodeIds = (0 ..< 100).mapIt(NodeId.example(rng))

  var
    tp: Taskpool
    ds: SQLiteKVStore
    manager: ProvidersManager

  setup:
    tp = Taskpool.new(num_threads = 4)
    ds = SQLiteKVStore.new(SqliteMemory, tp).tryGet()
    manager = ProvidersManager.new(ds)

    for id in nodeIds:
      for p in providers:
        (await manager.add(id, p)).tryGet

  teardown:
    (await ds.close()).tryGet()
    tp.shutdown()
    ds = nil
    manager = nil

  test "Should retrieve multiple records":
    for id in nodeIds:
      check:
        (await manager.get(id)).tryGet.len == 10

  test "Should retrieve multiple records with limit":
    for id in nodeIds:
      check:
        (await manager.get(id, 5)).tryGet.len == 5

  test "Should remove by NodeId":
    (await (manager.remove(nodeIds[0]))).tryGet
    (await (manager.remove(nodeIds[49]))).tryGet
    (await (manager.remove(nodeIds[99]))).tryGet

    check:
      nodeIds[0] notin manager.cache.cache
      not (await manager.contains(nodeIds[0]))

      nodeIds[49] notin manager.cache.cache
      not (await manager.contains(nodeIds[49]))

      nodeIds[99] notin manager.cache.cache
      not (await manager.contains(nodeIds[99]))

  test "Should remove by PeerId":
    (await (manager.remove(providers[0].data.peerId, true))).tryGet
    (await (manager.remove(providers[5].data.peerId, true))).tryGet
    (await (manager.remove(providers[9].data.peerId, true))).tryGet

    for id in nodeIds:
      check:
        providers[0].data.peerId notin manager.cache.cache.get(id).get
        not (await manager.contains(id, providers[0].data.peerId))

        providers[5].data.peerId notin manager.cache.cache.get(id).get
        not (await manager.contains(id, providers[5].data.peerId))

        providers[9].data.peerId notin manager.cache.cache.get(id).get
        not (await manager.contains(id, providers[9].data.peerId))

    check:
      not (await manager.contains(providers[0].data.peerId))
      not (await manager.contains(providers[5].data.peerId))
      not (await manager.contains(providers[9].data.peerId))

suite "Test Provider Maintenance":
  let
    rng = newDrbg()
    privKeys = (0 ..< 10).mapIt(PrivateKey.example(rng))
    providers = privKeys.mapIt(it.toSignedPeerRecord())
    nodeIds = (0 ..< 100).mapIt(NodeId.example(rng))

  var
    tp: Taskpool
    ds: SQLiteKVStore
    manager: ProvidersManager

  setupAll:
    tp = Taskpool.new(num_threads = 4)
    ds = SQLiteKVStore.new(SqliteMemory, tp).tryGet()
    manager = ProvidersManager.new(ds, disableCache = true)

    for id in nodeIds:
      for p in providers:
        (
          await manager.add(
            id, p, ttl = initDuration(milliseconds = 1).inMilliseconds()
          )
        ).tryGet

  teardownAll:
    (await ds.close()).tryGet()
    tp.shutdown()
    ds = nil
    manager = nil

  test "Should cleanup expired":
    for id in nodeIds:
      check:
        (await manager.get(id)).tryGet.len == 10

    await sleepAsync(500.millis)
    (await manager.store.cleanupExpired()).tryGet()

    for id in nodeIds:
      check:
        (await manager.get(id)).tryGet.len == 0

  test "Should not cleanup unexpired":
    let unexpired = PrivateKey.example(rng).toSignedPeerRecord()

    (
      await manager.add(
        nodeIds[0], unexpired, ttl = initDuration(minutes = 1).inMilliseconds()
      )
    ).tryGet

    await sleepAsync(500.millis)
    (await manager.store.cleanupExpired()).tryGet()

    let unexpiredProvs = (await manager.get(nodeIds[0])).tryGet

    check:
      unexpiredProvs.len == 1
      await (unexpired.data.peerId in manager)

    (await manager.remove(nodeIds[0])).tryGet

  test "Should cleanup orphaned":
    for id in nodeIds:
      check:
        (await manager.get(id)).tryGet.len == 0

    (await manager.store.cleanupOrphaned()).tryGet()

    for p in providers:
      check:
        not (await manager.contains(p.data.peerId))
